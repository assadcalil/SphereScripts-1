// Ultima Online Brasil - Script Pack
// Pack rebuilded by: Thiago Calil
VERSION=0.56d

////////
//Kegs//
////////

[ITEMDEF i_keg_potion]
ID=i_keg_wood
NAME=empty potion Keg
TYPE=t_script
VALUE={200 300}
WEIGHT=40
RESOURCES=8 i_board,1 i_barrel_tap,1 i_barrel_hoops
SKILLMAKE=TINKERING 65.0,t_tinker_tools
CATEGORY=Magic
SUBSECTION=Potions (keg)
DESCRIPTION=Empty Potion Keg
ON=@Create
   MOREP=0 0 0   //number of potions stored in keg
   MORE1=0       //store BASEID of first potion (i_potion_heal)
   MORE2=0      //store skillvalue off added potion
ON=@DClick
   SRC.SYSMESSAGE What potion would you like to fill the keg with?
   TARGET
   RETURN 1
ON=@TargOn_Item
   IF (<SRC.ACTION> != -1 )
      SRC.SYSMESSAGE You can't use this yet!
      RETURN 1
   ELIF (<src.targ.baseid>==i_bottle_empty)
      IF (0 >= <MOREX>)
         SRC.SYSMESSAGE You can't use this on a empty keg!
         RETURN 1
      ENDIF
      IF (<SRC.TARG.TOPOBJ.UID> != <SRC.UID>)
         SRC.SYSMESSAGE The empty bottle should be in your backpack!
         RETURN 1
      ENDIF
      SRC.SYSMESSAGE You fill an empty bottle.
      SOUND=snd_Z_END_LIQUID
      SERV.NEWITEM=<MORE1>
      NEW.MORE2=<MORE2> //restore skillvalue
      NEW.ATTR=0
      NEW.TIMER=-1
      NEW.BOUNCE
      MOREX -= 1
      IF (<MOREX>==0)
         NAME=     //set original name
         MORE1=0   //clear potion id
         MORE2=0   //clear makeskillvalue
      ELSE
         NAME=a <NEW.NAME> Keg(<EVAL <MOREX>>)
      ENDIF
      SRC.TARG.CONSUME 1
      RETURN 1
   ELIF (<SRC.TARG.TYPE> != t_potion)
      SRC.SYSMESSAGE Thats not a potion!
      RETURN 1
   ELIF (<MORE1>) && (<SRC.TARG.BASEID>!=<MORE1>)
      SRC.SYSMESSAGE You cannot mix potion types!
      RETURN 1
   ELIF (<MOREX> > 99) //max 100 potion
      SRC.SYSMESSAGE The keg is full!
      RETURN 1
   ELIF (<MOREX>==0)
      MORE1=<SRC.TARG.BASEID> // store potion baseid i_potion_xxx
   ELIF (<MORE1>!=<SRC.TARG.BASEID>)
      SRC.SYSMESSAGE You can't fill up the keg with this potions!
      RETURN 1
   ENDIF
   MORE2=(<MORE2>*<MOREX>)
   MOREX=(<MOREX> + 1)
   MORE2=(<MORE2> + <SRC.TARG.MORE2>) //store average makeskillvalue
   MORE2=(<MORE2> / <MOREX>)
   NAME=a <SRC.TARG.NAME> Keg(<MOREX>)
   SRC.SOUND=snd_Z_END_LIQUID
   SRC.SYSMESSAGE You fill up the keg with a <SRC.TARG.NAME> Potion!
   SRC.TARG.REMOVE
   SERV.NEWITEM=i_bottle_empty
   NEW.ATTR=0
   NEW.TIMER=-1
   NEW.BOUNCE
   RETURN 1